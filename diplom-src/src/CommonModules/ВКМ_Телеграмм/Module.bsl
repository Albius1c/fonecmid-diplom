#Область СлужебныеПроцедурыИФункции

Процедура РегламентОтправкаОповещенийВТелеграмм()Экспорт

	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ОтправкаСерверныхОповещенийКлиентам);

	РезультатЗапроса = ПолучитьДаныеКОтправке();
	Если ЗначениеЗаполнено(РезультатЗапроса) Тогда
		ОтправкаОповещенийВТелеграмм(РезультатЗапроса);
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьДаныеКОтправке()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВКМ_УведомленияТелеграмБоту.Наименование,
	|	ВКМ_УведомленияТелеграмБоту.ТекстСообщения,
	|	ВКМ_УведомленияТелеграмБоту.Ссылка
	|ИЗ
	|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту
	|ГДЕ
	|	НЕ ВКМ_УведомленияТелеграмБоту.ПометкаУдаления";

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Процедура ОтправкаОповещенийВТелеграмм(РезультатЗапроса)

	Для Каждого Строка Из РезультатЗапроса Цикл
		Если ЗначениеЗаполнено(Строка.ТекстСообщения) Тогда

			Ответ = СоединениеHTTP(Строка.ТекстСообщения);
			Если Ответ.КодСостояния <> 200 Тогда
				ТекстСообщения = "При обращении к Телеграмм что-то пошло не так. Повторите через 10 минут";
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			Иначе
				ТекущийОбъект = Строка.Ссылка.ПолучитьОбъект();
				ТекущийОбъект.Удалить();
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;
КонецПроцедуры
Функция СоединениеHTTP(ТекстСообщения)

	Соединение = Новый HTTPСоединение("api.telegram.org", 443, , , , , Новый ЗащищенноеСоединениеOpenSSL);

	Токен = Константы.ВКМ_ТокенУправленияТелеграммБотом.Получить();
	ИдетификаторЧата = Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить();

	АдресРесурса = "/bot" + Токен + "/sendMessage";

	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");

	ТелоЗапроса = СоздатьТелоЗапроса(ТекстСообщения, ИдетификаторЧата);
	ТелоJSON = ЗаписатьЗначаниеВJSON(ТелоЗапроса);

	Запрос  = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	Запрос.УстановитьТелоИзСтроки(ТелоJSON);

	Возврат Соединение.ВызватьHTTPМетод("POST", Запрос);

КонецФункции

Функция СоздатьТелоЗапроса(ТекстСообщения, ИдетификаторЧата)

	ТелоОбъекта = Новый Структура;
	ТелоОбъекта.Вставить("chat_id", ИдетификаторЧата);
	ТелоОбъекта.Вставить("text", ТекстСообщения);

	Возврат ТелоОбъекта;

КонецФункции

Функция ЗаписатьЗначаниеВJSON(СтруктураЗаписи) Экспорт

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, СтруктураЗаписи);
	СтрокаJSON = ЗаписьJSON.Закрыть();

	Возврат СтрокаJSON;
КонецФункции

#КонецОбласти