#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Ответственный = Пользователи.ТекущийПользователь();

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения);
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	СуммаДокумента = Товары.Итог("Сумма") + Услуги.Итог("Сумма");

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОбработкаЗаказов.Записывать = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.ВыполненныеКлиентуРаботы.Записывать = Истина;
	Движение = Движения.ОбработкаЗаказов.Добавить();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Договор = Договор;
	Движение.Заказ = Основание;
	Движение.СуммаОтгрузки = СуммаДокумента;

	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Сумма = ТекСтрокаТовары.Сумма;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;

//Дауге 05022024 ++
	Для Каждого ТекСтрокаУслуги Из Услуги Цикл
		Если Константы.ВКМ_НоменклатураРаботыСпециалиста.Получить() = ТекСтрокаУслуги.Номенклатура Тогда
			Движение = Движения.ВыполненныеКлиентуРаботы.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Договор = Договор;
			Движение.Клиент = Контрагент;
			Движение.Регистратор = Ссылка;
			Движение.СуммаКОплате = ТекСтрокаУслуги.Сумма;
			Движение.КоличествоЧасов = ТекСтрокаУслуги.Количество;
		КонецЕсли;
	КонецЦикла;
	//Дауге 05022024 --

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	ЗаказПокупателя.Организация КАК Организация,
				   |	ЗаказПокупателя.Контрагент КАК Контрагент,
				   |	ЗаказПокупателя.Договор КАК Договор,
				   |	ЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
				   |	ЗаказПокупателя.Товары.(
				   |		Ссылка КАК Ссылка,
				   |		НомерСтроки КАК НомерСтроки,
				   |		Номенклатура КАК Номенклатура,
				   |		Количество КАК Количество,
				   |		Цена КАК Цена,
				   |		Сумма КАК Сумма
				   |	) КАК Товары,
				   |	ЗаказПокупателя.Услуги.(
				   |		Ссылка КАК Ссылка,
				   |		НомерСтроки КАК НомерСтроки,
				   |		Номенклатура КАК Номенклатура,
				   |		Количество КАК Количество,
				   |		Цена КАК Цена,
				   |		Сумма КАК Сумма
				   |	) КАК Услуги
				   |ИЗ
				   |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
				   |ГДЕ
				   |	ЗаказПокупателя.Ссылка = &Ссылка";

	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);

	Выборка = Запрос.Выполнить().Выбрать();

	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);

	ТоварыОснования = Выборка.Товары.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Товары.Добавить(), ТоварыОснования);
	КонецЦикла;

	УслугиОснования = Выборка.Услуги.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Услуги.Добавить(), УслугиОснования);
	КонецЦикла;

	Основание = ДанныеЗаполнения;

КонецПроцедуры

//Дауге 04022024++
Процедура ВКМ_ВыполнитьАвтозаполнение() Экспорт

	ВКМ_НоменклатураАбонентскаяПлата = Константы.ВКМ_НоменклатураАбонентскаяПлата.Получить();
	ВКМ_НоменклатураРаботыСпециалиста = Константы.ВКМ_НоменклатураРаботыСпециалиста.Получить();

	Если Не ЗначениеЗаполнено(ВКМ_НоменклатураАбонентскаяПлата) Или Не ЗначениеЗаполнено(
		ВКМ_НоменклатураРаботыСпециалиста) Тогда
		ВызватьИсключение НСтр(
			"ru='Не заполнены константы ""Номенклатура абонентская Плата"" или ""Номенклатура работы специалиста""'");
	КонецЕсли;

	Услуги.Очистить();

	ВКМ_ЗаполнитьСуммуАбоненскойПлаты(ВКМ_НоменклатураАбонентскаяПлата);
	ВКМ_ЗаполнитьСуммуРаботаСпециалиста(ВКМ_НоменклатураРаботыСпециалиста);

	Если Не Ссылка.Пустая() Тогда
		ВКМ_ЗаполнитьТабличнуюЧастьДокумента();
	КонецЕсли;

КонецПроцедуры

Процедура ВКМ_ЗаполнитьСуммуАбоненскойПлаты(НоменклатураАбонентскаяПлат)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.ВМК_СуммаАбоненскойПлаты КАК СуммаАбоненскойПлаты
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка = &Договор";

	Запрос.УстановитьПараметр("Договор", Договор);

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Если Выборка.Следующий() Тогда
		ЦенаУслуги = Выборка.СуммаАбоненскойПлаты;
	КонецЕсли;

	Если ЗначениеЗаполнено(ЦенаУслуги) Тогда

		НоваяСтрока = Услуги.Добавить();
		НоваяСтрока.Номенклатура = НоменклатураАбонентскаяПлат;
		НоваяСтрока.Цена = ЦенаУслуги;
		НоваяСтрока.Количество = 1;
		НоваяСтрока.Сумма = НоваяСтрока.Цена * НоваяСтрока.Количество;

	КонецЕсли;

КонецПроцедуры

Процедура ВКМ_ЗаполнитьСуммуРаботаСпециалиста(НоменклатураРаботыСпециалиста)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(ВыполненныеКлиентуРаботыОстатки.КоличествоЧасовОстаток) * -1 КАК КоличествоЧасов,
	|	СУММА(ВыполненныеКлиентуРаботыОстатки.СуммаКОплатеОстаток) * -1 КАК СуммаКОплатеОстаток
	|ИЗ
	|	РегистрНакопления.ВыполненныеКлиентуРаботы.Остатки(&Период, Договор = &Договор) КАК ВыполненныеКлиентуРаботыОстатки
	|ГДЕ
	|	ВыполненныеКлиентуРаботыОстатки.КоличествоЧасовОстаток < 0";

	Запрос.УстановитьПараметр("Период", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Договор", Договор);

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	Если Выборка.Следующий() Тогда
		ЦенаУслуги = Выборка.СуммаКОплатеОстаток;
		КоличествоЧасов = Выборка.КоличествоЧасов;
	КонецЕсли;

	Если ЗначениеЗаполнено(КоличествоЧасов) Тогда

		НоваяСтрока = Услуги.Добавить();
		НоваяСтрока.Номенклатура = НоменклатураРаботыСпециалиста;
		НоваяСтрока.Сумма =  ЦенаУслуги;
		НоваяСтрока.Количество = КоличествоЧасов;
		НоваяСтрока.Цена = НоваяСтрока.Сумма / ?(НоваяСтрока.Количество <> 0, НоваяСтрока.Количество, 1);

	КонецЕсли;

КонецПроцедуры

Процедура ВКМ_ЗаполнитьТабличнуюЧастьДокумента()

	Если Не ЗначениеЗаполнено(Услуги) Тогда
		Возврат;
	КонецЕсли;

	ДокументОбъект = Ссылка.ПолучитьОбъект();
	ДокументОбъект.Услуги.Очистить();

	Для Каждого Строка Из Услуги Цикл
		НоваяСтрока = ДокументОбъект.Услуги.Добавить();
		НоваяСтрока.Номенклатура = Строка.Номенклатура;
		НоваяСтрока.Цена = Строка.Цена;
		НоваяСтрока.Количество = Строка.Количество;
		НоваяСтрока.Сумма = НоваяСтрока.Цена * НоваяСтрока.Количество;
	КонецЦикла;

КонецПроцедуры
//Дауге 04022024--

#КонецОбласти

#КонецЕсли